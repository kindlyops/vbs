load("@io_bazel_rules_go//go:def.bzl", "go_binary", "go_library", "go_test")
load("@bazel_gazelle//:def.bzl", "gazelle")
load("//:version.bzl", "VERSION")
load("@bazel_skylib//rules:copy_file.bzl", "copy_file")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_push", "oci_tarball")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")

package(default_visibility = ["//visibility:public"])

# gazelle:prefix github.com/kindlyops/vbs
# gazelle:exclude dummy.go
# gazelle:resolve go github.com/kindlyops/vbs/embeddy //embeddy:go_default_library
gazelle(
    name = "gazelle",
    external = "vendored",
)

go_library(
    name = "go_default_library",
    srcs = ["main.go"],
    importpath = "github.com/kindlyops/vbs",
    visibility = ["//visibility:private"],
    deps = ["//cmd:go_default_library"],
)

go_binary(
    name = "vbs-linux",
    embed = [":go_default_library"],
    goarch = "amd64",
    goos = "linux",
    visibility = ["//visibility:public"],
    x_defs = {
        "version": VERSION,
    },
)

go_binary(
    name = "vbs-linux-arm",
    embed = [":go_default_library"],
    goarch = "arm64",
    goos = "linux",
    visibility = ["//visibility:public"],
    x_defs = {
        "version": VERSION,
    },
)

go_binary(
    name = "vbs-darwin",
    embed = [":go_default_library"],
    goarch = "amd64",
    goos = "darwin",
    visibility = ["//visibility:public"],
    x_defs = {
        "version": VERSION,
    },
)

go_binary(
    name = "vbs-darwin-m1",
    embed = [":go_default_library"],
    goarch = "arm64",
    goos = "darwin",
    visibility = ["//visibility:public"],
    x_defs = {
        "version": VERSION,
    },
)

go_binary(
    name = "vbs-windows",
    embed = [":go_default_library"],
    goarch = "amd64",
    goos = "windows",
    visibility = ["//visibility:public"],
    x_defs = {
        "version": VERSION,
    },
)

# Package the binary into a tar for the OCI image
pkg_tar(
    name = "vbs_tar",
    srcs = [":vbs-linux"],
    package_dir = "/usr/bin",
)

# Create OCI image
oci_image(
    name = "vbs_image",
    base = "@distroless_static",
    tars = [":vbs_tar"],
    entrypoint = ["/usr/bin/vbs-linux"],
    cmd = [
        "serve",
        "--http=0.0.0.0:8080",
    ],
    exposed_ports = ["8080"],
    labels = {
        "org.opencontainers.image.source": "https://github.com/kindlyops/vbs",
        "org.opencontainers.image.description": "Tools for video broadcast production",
    },
)

# Generate tag file with IMAGE_TAG variable
genrule(
    name = "image_tags",
    outs = ["image_tags.txt"],
    cmd = "echo '$(IMAGE_TAG)' > $@",
    stamp = 1,
    tags = ["manual"],
)

# Push to registry
oci_push(
    name = "push_image",
    image = ":vbs_image",
    repository = "ghcr.io/kindlyops/vbs",
    remote_tags = ":image_tags",
    tags = ["manual"],
)

alias(
    name = "vbs",
    actual = select({
        "@io_bazel_rules_go//go/platform:linux_amd64": ":vbs-linux",
        "@io_bazel_rules_go//go/platform:linux_arm64": ":vbs-linux-arm",
        "@io_bazel_rules_go//go/platform:darwin_amd64": ":vbs-darwin",
        "@io_bazel_rules_go//go/platform:darwin_arm64": ":vbs-darwin-m1",
        "@io_bazel_rules_go//go/platform:windows_amd64": ":vbs-windows",
        "//conditions:default": ":vbs-linux",
    }),
)

copy_file(
    name = "install-darwin",
    src = "//:vbs-darwin",
    out = "bdist/vbs_darwin_amd64/vbs",
)

copy_file(
    name = "install-darwin-m1",
    src = "//:vbs-darwin-m1",
    out = "bdist/vbs_darwin_arm64/vbs",
)

copy_file(
    name = "install-linux",
    src = "//:vbs-linux",
    out = "bdist/vbs_linux_amd64/vbs",
)

copy_file(
    name = "install-linux-arm",
    src = "//:vbs-linux-arm",
    out = "bdist/vbs_linux_arm64/vbs",
)

copy_file(
    name = "install-windows",
    src = "//:vbs-windows",
    out = "bdist/vbs_windows_amd64/vbs.exe",
)

go_test(
    name = "go_default_test",
    size = "small",
    srcs = ["main_test.go"],
    args = ["-cli"] + select({
        "@io_bazel_rules_go//go/platform:linux_amd64": [
            "$(location :vbs-linux)",
        ],
        "@io_bazel_rules_go//go/platform:linux_arm64": [
            "$(location :vbs-linux-arm)",
        ],
        "@io_bazel_rules_go//go/platform:darwin_amd64": [
            "$(location :vbs-darwin)",
        ],
        "@io_bazel_rules_go//go/platform:darwin_arm64": [
            "$(location :vbs-darwin-m1)",
        ],
        "@io_bazel_rules_go//go/platform:windows_amd64": [
            "$(location :vbs-windows)",
        ],
        "//conditions:default": [],
    }),
    data = select({
        "@io_bazel_rules_go//go/platform:linux_amd64": [
            ":vbs-linux",
        ],
        "@io_bazel_rules_go//go/platform:linux_arm64": [
            ":vbs-linux-arm",
        ],
        "@io_bazel_rules_go//go/platform:darwin_amd64": [
            ":vbs-darwin",
        ],
        "@io_bazel_rules_go//go/platform:darwin_arm64": [
            ":vbs-darwin-m1",
        ],
        "@io_bazel_rules_go//go/platform:windows_amd64": [
            ":vbs-windows",
        ],
        "//conditions:default": [],
    }),
    rundir = ".",
)

sh_binary(
    name = "vendor",
    srcs = ["vendor.sh"],
    args = [
        "$(location @bazel_gazelle//cmd/gazelle)",
    ],
    data = [
        "@bazel_gazelle//cmd/gazelle",
        "@go_sdk//:files",
    ],
    tags = ["manual"],
    visibility = ["//visibility:public"],
)

sh_binary(
    name = "lint",
    srcs = ["lint.sh"],
    args = [
        "$(location @bazel_gazelle//cmd/gazelle)",
    ],
    data = [
        "@bazel_gazelle//cmd/gazelle",
        "@go_sdk//:files",
    ],
    tags = ["manual"],
    visibility = ["//visibility:public"],
)
